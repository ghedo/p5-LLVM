package inc::MakeMaker;

use Moose;

extends 'Dist::Zilla::Plugin::MakeMaker::Awesome';

override _build_MakeFile_PL_template => sub {
	my ($self) = @_;

	my $template = <<'TEMPLATE';
use File::Which;
use Devel::CheckLib;

my $llvmc = $ENV{LLVM_CONFIG} || 'llvm-config';
unless (which($llvmc)) { print "Can't find \"$llvmc\"\n"; exit }

my $vers = `$llvmc --version`; chomp $vers;
#unless ($vers eq '3.1') { print "Wrong LLVM version $vers: needed 3.1\n"; exit }

my ($rev, $maj, $min) = split /\./, $vers;
$min ||= 0;
my $define = "-DLLVM_REV=$rev -DLLVM_MAJ=$maj -DLLVM_MIN=$min";

# This Makefile.PL for {{ $distname }} was generated by Dist::Zilla.
# Don't edit it but the dist.ini used to construct it.
{{ $perl_prereq ? qq[BEGIN { require $perl_prereq; }] : ''; }}
use strict;
use warnings;
use Config;
use ExtUtils::MakeMaker {{ $eumm_version }};
{{ $share_dir_block[0] }}
my {{ $WriteMakefileArgs }}

my $ccflags = `$llvmc --cflags`; chomp $ccflags;
$WriteMakefileArgs{CCFLAGS} = $ccflags;
my $libs = `$llvmc --ldflags --libs --system-libs`
  or die "$llvmc failed to return library info\n";
$libs =~ tr/\n\r/ /s;
if ($libs =~ /$Config{_a}\b/) {
  print "llvm-config is trying a static build, ignoring it and looking for the .so\n";
  undef $libs;
  my @comps = ( $rev, $maj, $min );
  my $ldflags = `$llvmc --ldflags`; # to get any -L
  chomp $ldflags;
  my @paths = map substr($_, 2), grep /^-L/, split ' ', $ldflags;
  while (@comps) {
    my $name = "LLVM-".join(".", @comps);
    if (check_lib(lib => $name,
                  libpath => \@paths)) {
      $libs = "$ldflags -l$name";
      last;
    }
    pop @comps;
  }
  unless ($libs) {
    if (check_lib(lib => "LLVM",
                  libpath => \@paths)) {
      $libs = "$ldflags -lLLVM";
    }
    else {
      die "No LLVM library found\n";
    }
  }
}
$WriteMakefileArgs{LIBS} = "$libs";
$WriteMakefileArgs{DEFINE} = $define;

unless ( eval { ExtUtils::MakeMaker->VERSION(6.56) } ) {
  my $br = delete $WriteMakefileArgs{BUILD_REQUIRES};
  my $pp = $WriteMakefileArgs{PREREQ_PM};
  for my $mod ( keys %$br ) {
    if ( exists $pp->{$mod} ) {
      $pp->{$mod} = $br->{$mod} if $br->{$mod} > $pp->{$mod};
    }
    else {
      $pp->{$mod} = $br->{$mod};
    }
  }
}

delete $WriteMakefileArgs{CONFIGURE_REQUIRES}
  unless eval { ExtUtils::MakeMaker->VERSION(6.52) };

WriteMakefile(%WriteMakefileArgs);
{{ $share_dir_block[1] }}
TEMPLATE

	return $template;
};

override _build_WriteMakefile_args => sub {
	return +{
		%{ super() },
		INC	=> '-I.',
		OBJECT	=> '$(O_FILES)',
	}
};

__PACKAGE__ -> meta -> make_immutable;
